buildscript {
    repositories {
        maven {
            jcenter()
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

description='Spring 5 Recipes - Sources'

allprojects {
    group = 'com.apress.springrecipes'


    // Define repositorie to get dependencies
    repositories {
        mavenCentral()
        // Public Spring artifacts
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/libs-snapshot" }
        maven { url "https://repo.spring.io/libs-release"}
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

subprojects { subproject ->

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.github.ben-manes.versions'

    ext['spring.version'] = '5.0.0.RC4'
    ext['spring-batch.version'] = '4.0.0.M3'
    ext['spring-security.version'] = '5.0.0.M4'
    ext['spring-integration.version'] = '5.0.0.M7'
    ext['spring-social.version'] = '2.0.0.M4'
    ext['spring-social-twitter.version'] = '2.0.0.M4'
    ext['spring-social-facebook.version'] = '3.0.0.M3'
    ext['spring-data-releasetrain.version'] = 'Kay-RC3'
    ext['spring-kafka.version'] = '2.0.0.RC1'
    ext['spring-amqp.version'] = '2.0.0.RC1'
    ext['thymeleaf.version'] = '3.0.7.RELEASE'
    ext['mockito.version'] = '2.10.0'
    ext['servlet-api.version'] = '4.0.0'
    ext['springIOPlatform'] = 'Cairo-BUILD-SNAPSHOT'
    ext['apacheCxfVersion'] = '3.1.13'
    ext['neo4jVersion'] = '3.1.6'

	compileJava.options*.compilerArgs = [
		"-Xlint:serial", "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
		"-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
		"-Xlint:overrides", "-Xlint:path", "-Xlint:processing", "-Xlint:static",
		"-Xlint:try", "-Xlint:fallthrough", "-Xlint:rawtypes", "-Xlint:deprecation",
		"-Xlint:unchecked", "-Xlint:-options"
	]

	compileTestJava.options*.compilerArgs = [
		"-Xlint:serial", "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
		"-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
		"-Xlint:overrides", "-Xlint:path", "-Xlint:processing", "-Xlint:static",
		"-Xlint:try", "-Xlint:-fallthrough", "-Xlint:-rawtypes", "-Xlint:-deprecation",
		"-Xlint:-unchecked", "-Xlint:-options"]

    sourceCompatibility=1.8
    targetCompatibility=1.8

	configurations {
		// We want to use SLF4J for logging, ban commons-logging
		all*.exclude group: 'commons-logging', module: 'commons-logging'
	}

    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:$springIOPlatform"
        }
    }

	dependencies {
		compile("org.slf4j:slf4j-api")
		compile("org.slf4j:jcl-over-slf4j")
		compile("ch.qos.logback:logback-classic")

    	// Add dependencies for testing (these will only be used for the testing phase)
		testCompile("junit:junit")
		testCompile("org.mockito:mockito-core")
        testCompile("org.assertj:assertj-core")
		testCompile("org.springframework:spring-test")
	}

	task copyDependenciesToLibDir( type: Copy ) {
		into "$buildDir/libs/lib"
		from configurations.runtime
	}

    jar {
        // Copy jar dependencies and add to Class-Path.
        dependsOn copyDependenciesToLibDir
        def deps = fileTree(dir: "$buildDir/libs/lib", include: '*.jar')
        manifest.attributes("Class-Path": deps.files.collect {'lib/'+it.name}.join(' '))
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}
