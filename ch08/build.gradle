description='Spring Recipes :: Chapter 8 :: Spring Mobile'

buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

subprojects { subproject ->

    // Define usage of war plugin
    apply plugin: 'war'
    apply plugin: 'docker'
    // Define defaulttasks, first clean, then build, then war
    defaultTasks 'clean','build','war'
    // Define dependencies
    war {
        archiveName='mobile.war'
    }

    dependencies {
        compile group: 'org.springframework', name: 'spring-context', version:"$springVersion"
        compile group: 'org.springframework', name: 'spring-webmvc', version:"$springVersion"
        compile group: 'org.springframework.mobile', name: 'spring-mobile-device', version:"$springMobileVersion"

        compile group: 'org.apache.taglibs', name: 'taglibs-standard-spec', version: '1.2.1'
        compile group: 'org.apache.taglibs', name: 'taglibs-standard-impl', version: '1.2.1'

        providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: "$servletApiVersion"
    }

    task buildDocker(type: Docker, dependsOn: build) {
        push = false
        applicationName = war.archiveName
        tag = 'spring-recipes-4th/mobile'
        tagVersion = 'latest'
        dockerfile = file('../Dockerfile')
        doFirst {
            copy {
                from war
                into stageDir
            }
        }
    }


}

